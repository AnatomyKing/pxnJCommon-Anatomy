// pxnCommon

plugins {
	id 'java'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '1.2.2'
}



version = '3.6.1'
group   = 'com.poixson'
defaultTasks 'clean', 'build', 'shadow'

def jlineVersion     = '2.13'
def snakeyamlVersion = '1.16'
def jclVersion       = '2.6'
def junitVersion     = '4.12'



sourceCompatibility = 1.8
targetCompatibility = 1.8

[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'



sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'resources/'
		}
	}
}



jar {
	archiveName = "${project.name}-${project.version}-sparse.jar"
	// write app.properties file
	Properties props = new Properties()
	File propsFile = new File('resources/app.properties')
	props.load(propsFile.newDataInputStream())
	props.setProperty('name',       "${project.name}")
	props.setProperty('title',      "${project.name}")
	props.setProperty('version',    "${project.version}")
	props.setProperty('url',        'http://growcontrol.com')
	props.setProperty('org_name',   'PoiXson')
	props.setProperty('org_url',    'http://poixson.com')
	props.setProperty('issue_name', 'github')
	props.setProperty('issue_url',  'https://github.com/GrowControl/pxnCommon/issues')
	props.store(propsFile.newWriter(), null)
}
shadowJar {
	archiveName = "${project.name}-${project.version}.jar"
}



repositories {
	jcenter()
}
dependencies {
	compile "jline:jline:${jlineVersion}"
	compile "org.yaml:snakeyaml:${snakeyamlVersion}"
	compile "org.xeustechnologies:jcl-core:${jclVersion}"
	testCompile "junit:junit:${junitVersion}"
}



configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		cacheDynamicVersionsFor 5*60, 'seconds'
		cacheChangingModulesFor 0,    'seconds'
	}
}



task sourceJar(type: Jar, dependsOn: classes) {
	archiveName = "${project.name}-${project.version}-sources.jar"
	classifier  = 'sources'
	from sourceSets.main.allSource
}
artifacts {
	archives sourceJar
}



task copy (type: Copy) {
	def path = new File('../output/')
	path.mkdirs()
	copy {
		from "build/libs/${project.name}-${project.version}.jar"
		into '../output/'
	}
	copy {
		from "build/libs/${project.name}-${project.version}-sources.jar"
		into '../output/'
	}
}
