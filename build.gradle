// pxnCommon

plugins {
	id 'java'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '1.2.2'
}

version = '3.6.1'
group = 'com.poixson'
defaultTasks 'clean', 'build', 'shadow'

def jlineVersion     = '2.13'
def snakeyamlVersion = '1.16'
def jclVersion       = '2.6'
def junitVersion     = '4.12'

[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'resources'
		}
	}
}

jar {
	archiveName = "${project.name}-${project.version}-sparse.jar"
}

shadowJar {
	archiveName = "${project.name}-${project.version}.jar"
//	append('README')
//	dependencies {
//		exclude(dependency('asm:asm:3.3.1'))
//	}
}

repositories {
	jcenter()
	//mavenCentral()
}

dependencies {
	compile "jline:jline:${jlineVersion}"
	compile "org.yaml:snakeyaml:${snakeyamlVersion}"
	compile "org.xeustechnologies:jcl-core:${jclVersion}"
	testCompile "junit:junit:${junitVersion}"
}

configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		cacheDynamicVersionsFor 5*60, 'seconds'
		cacheChangingModulesFor 0, 'seconds'
	}
}

task sourceJar(type: Jar, dependsOn: classes) {
	archiveName = "${project.name}-${project.version}-sources.jar"
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourceJar
}
